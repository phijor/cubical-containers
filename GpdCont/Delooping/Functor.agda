{-# OPTIONS --lossy-unification #-}
module GpdCont.Delooping.Functor where

open import GpdCont.Prelude

open import GpdCont.Group.MapConjugator using (Conjugator ; idConjugator ; compConjugator)
open import GpdCont.Group.TwoCategory using (TwoGroup)

open import GpdCont.TwoCategory.Base
open import GpdCont.TwoCategory.Pseudofunctor
open import GpdCont.TwoCategory.HomotopyGroupoid using (hGpdCat)
open import GpdCont.TwoCategory.LocalCategory using (LocalCategory)
open import GpdCont.TwoCategory.LocalFunctor using (LocalFunctor)

import      GpdCont.Delooping as Delooping
open import GpdCont.Delooping.Map as Map using (map ; mapâ‰¡ ; module MapPathEquiv)

open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.GroupoidLaws as GL using (compPathRefl)

open import Cubical.Algebra.Group
open import Cubical.Algebra.Group.Morphisms using (GroupHom ; GroupEquiv)
open import Cubical.Algebra.Group.MorphismProperties renaming (compGroupHom to _âˆ™Grp_ ; compGroupEquiv to _âˆ™GrpE_)

open import Cubical.Categories.Category.Base using (CatIso ; pathToIso)
open import Cubical.Categories.Functor.Base using (Functor)
open import Cubical.Categories.Equivalence.WeakEquivalence

-- Delooping is a locally essentially surjective functor:
-- The functorial action on 1-cells has a mere section.
module LocalInverse {â„“} {G H : Group â„“} where
  open import Cubical.HITs.PropositionalTruncation hiding (map)
  open import Cubical.HITs.PropositionalTruncation.Monad

  private
    open module H = GroupStr (str H) using (_Â·_)
    module G = GroupStr (str G)

    ğ”¹G = Delooping.ğ”¹ âŸ¨ G âŸ© (str G)
    ğ”¹H = Delooping.ğ”¹ âŸ¨ H âŸ© (str H)

    module ğ”¹G = Delooping âŸ¨ G âŸ© (str G)
    module ğ”¹H = Delooping âŸ¨ H âŸ© (str H)

  -- Any map (f : ğ”¹G â†’ ğ”¹H) is uniquely determined by the choice of
  -- a point (y : ğ”¹H) and a group homomorphism (Ï† : G â†’ Ï€â‚(ğ”¹H, y)).
  unrec-fun : (f : ğ”¹G â†’ ğ”¹H) â†’ Î£[ y âˆˆ ğ”¹H ] GroupHom G (ğ”¹H.Ï€â‚ y)
  unrec-fun = invEq (ğ”¹G.recEquivHom {X = ğ”¹H , ğ”¹H.isGroupoidğ”¹})

  -- We would like to define a group homomorphism G â†’ H from (f : ğ”¹G â†’ ğ”¹H)
  -- by inspecting which group elements in H correspond to paths
  --
  --    (ğ”¹G.loop â‹† f) : G â†’ Ï€â‚(ğ”¹H, f â‹†)
  --
  -- But (f â‹† : ğ”¹H) is *not* definitionally equal to (â‹† : ğ”¹H), therefore we
  -- cannot apply the equivalence (loop : H â‰ƒ Ï€â‚(ğ”¹H, â‹†)) to extract elements of H.
  --
  -- If we have access to a path (p : f â‹† â‰¡ â‹†), then we can conjugate by `p`:
  -- multiplication (Î» q â†’ pâ»Â¹ âˆ™ q âˆ™ p) induces an equivalence of groups
  --
  --    Ï€â‚(ğ”¹H, f â‹†) â‰ƒ Ï€â‚(ğ”¹H, â‹†),
  --
  -- and postcomposing with this equivalence, we obtain a group homomorphism
  --
  --                  Ï†                conj(p)             loopâ»Â¹
  --    unmap f : G ---->Â Ï€â‚(ğ”¹H, f â‹†) --------> Ï€â‚(ğ”¹H, â‹†) -------> H
  unmap : (f : ğ”¹G â†’ ğ”¹H) (p : f ğ”¹G.â‹† â‰¡ ğ”¹H.â‹†) â†’ GroupHom G H
  unmap f p using (y , Ï†) â† unrec-fun f = Ï† âˆ™Grp (GroupEquivâ†’GroupHom fixit) where
    conjEquiv : GroupEquiv (ğ”¹H.Ï€â‚ y) (ğ”¹H.Ï€â‚ ğ”¹H.â‹†)
    conjEquiv = ğ”¹H.conjugatePathEquiv p

    fixit : GroupEquiv (ğ”¹H.Ï€â‚ y) H
    fixit = conjEquiv âˆ™GrpE ğ”¹H.unloopGroupEquiv

  -- For any choice of path (p : f â‹† â‰¡ â‹†), we can show that this is a section of `map`.
  -- We construct the homotopy with `f` pointwise by induction on the domain.
  unmap-section : (f : ğ”¹G â†’ ğ”¹H) (p : f ğ”¹G.â‹† â‰¡ ğ”¹H.â‹†) â†’ map (unmap f p) â‰¡ f
  unmap-section f p using (y , (Ï† , _)) â† unrec-fun f = funExt ext where
    -- On the point, both `map` and `unmap` compute to the point in the codomain.
    -- Thus, pâ»Â¹ connects `â‹†` to `f â‹†`.
    extâ‹† : ğ”¹H.â‹† â‰¡ f ğ”¹G.â‹†
    extâ‹† = sym p

    -- For a loop in ğ”¹G defined by (g : G), we need to show that there
    -- is a filler for the square
    --
    --               cong (map (unmap f p)) (loop g)
    --        (f â‹†) --------------------------------- (f â‹†)
    --          |                                       |
    --          |                                       |
    --      pâ»Â¹ |                                       | pâ»Â¹
    --          |                                       |
    --          |                                       |
    --         (â‹†) ----------------------------------- (â‹†)
    --                      cong f (loop g)
    ext-loop : âˆ€ g â†’ Square (sym p) (sym p) (cong (map (unmap f p)) (ğ”¹G.loop g)) (Ï† g)
    ext-loop g =
      -- We observe that both the top and bottom side of this square simplify.
      subst (Î» top â†’ Square (sym p) (sym p) top (Ï† g)) (top-path g) (ext-loop' g) where

      -- First, Ï† is defined by induction from f.
      -- The bottom of the square is (definitionally) equal to (Ï† g).
      _ : âˆ€ g â†’ cong f (ğ”¹G.loop g) â‰¡ Ï† g
      _ = Î» g â†’ refl

      -- Secondly, on loops, unmap is defined as a conjugation, followed
      -- by the inverse to `loop : H â†’ Ï€â‚(ğ”¹H, â‹†)`:
      conjâ‚š : f ğ”¹G.â‹† â‰¡ f ğ”¹G.â‹† â†’ ğ”¹H.â‹† â‰¡ ğ”¹H.â‹†
      conjâ‚š = sym p âˆ™âˆ™_âˆ™âˆ™ p

      _ : âˆ€ g â†’ cong (map $ unmap f p) (ğ”¹G.loop g) â‰¡ ğ”¹H.loop (ğ”¹H.unloop (conjâ‚š (Ï† g)))
      _ = Î» g â†’ refl

      -- We thus substitute (conjâ‚š (Ï† g)) for the top path by cancelling loop and unloop, ...
      top-path : âˆ€ g â†’ conjâ‚š (Ï† g) â‰¡ cong (map $ unmap f p) (ğ”¹G.loop g)
      top-path g = loop-retract $ conjâ‚š (Ï† g) where
        loop-retract : âˆ€ h â†’ h â‰¡ ğ”¹H.loop (ğ”¹H.unloop h)
        loop-retract h = sym (retEq ğ”¹H.Î©Deloopingâ‰ƒ h)

      -- ...and are left to show that there's a filler for the square
      --
      --            pâ»Â¹ âˆ™âˆ™ (Ï† g) âˆ™âˆ™ p
      --     (f â‹†) ------------------- (f â‹†)
      --       |                         |
      --       |                         |
      --   pâ»Â¹ |                         | pâ»Â¹
      --       |                         |
      --       |                         |
      --      (â‹†) --------------------- (â‹†)
      --                   Ï† g
      --
      -- which follows from uniqueness of path composites.
      ext-loop' : âˆ€ g â†’ Square (sym p) (sym p) ((sym p) âˆ™âˆ™ (Ï† g) âˆ™âˆ™ p) (Ï† g)
      ext-loop' g i j = doubleCompPath-filler (sym p) (Ï† g) p (~ j) i

    ext : âˆ€ x â†’ map (unmap f p) x â‰¡ f x
    ext = ğ”¹G.elimSet (Î» x â†’ ğ”¹H.isGroupoidğ”¹ _ (f x)) extâ‹† ext-loop

  -- In general, there is a set of paths (f â‹† â‰¡ â‹†) from which we would
  -- habe to pick one in order to apply `unmap f`.  This is not posible
  -- in general without choice.  But since ğ”¹H is path-connected, we merely
  -- get such a path, thus merely a section to `map`.
  isSurjection-map : (f : ğ”¹G â†’ ğ”¹H) â†’ âˆƒ[ Ï† âˆˆ GroupHom G H ] map Ï† â‰¡ f
  isSurjection-map f = do
    -- ğ”¹H is path-connected, thus we merely get (p : f ğ”¹G.â‹† â‰¡ ğ”¹H.â‹†)
    p â† ğ”¹H.merePath (f ğ”¹G.â‹†) ğ”¹H.â‹†
    -- Conjugation by p gives us a group hom with the right endpoints
    âˆƒ-intro (unmap f p) (unmap-section f p)

module TwoFunc (â„“ : Level) where
  private
    variable
      G H K L : Group â„“
      Ï† Ïˆ Ï : GroupHom G H

    module TwoGroup = TwoCategory (TwoGroup â„“)
    module hGpdCat = TwoCategory (hGpdCat â„“)

    ğ”¹-ob : Group â„“ â†’ hGroupoid â„“
    ğ”¹-ob (G , G-str) = Delooping.ğ”¹ G G-str , Delooping.isGroupoidğ”¹

    ğ”¹-hom : {G H : Group â„“} â†’ GroupHom G H â†’ âŸ¨ ğ”¹-ob G âŸ© â†’ âŸ¨ ğ”¹-ob H âŸ©
    ğ”¹-hom Ï† = Map.map Ï†

    ğ”¹-rel : {G H : Group â„“} {Ï† Ïˆ : GroupHom G H} â†’ Conjugator Ï† Ïˆ â†’ ğ”¹-hom Ï† â‰¡ ğ”¹-homÂ Ïˆ
    ğ”¹-rel {Ï†} {Ïˆ} = mapâ‰¡ Ï† Ïˆ

    ğ”¹-rel-id : ğ”¹-rel (idConjugator Ï†) â‰¡ refl
    ğ”¹-rel-id {Ï†} = Map.mapâ‰¡-id-refl Ï†

    ğ”¹-rel-trans : (hâ‚ : Conjugator Ï† Ïˆ) (hâ‚‚ : Conjugator Ïˆ Ï) â†’ ğ”¹-rel (compConjugator hâ‚ hâ‚‚) â‰¡ ğ”¹-rel hâ‚ âˆ™ ğ”¹-rel hâ‚‚
    ğ”¹-rel-trans {Ï†} {Ïˆ} {Ï} = Map.mapâ‰¡-comp-âˆ™ Ï† Ïˆ Ï

    ğ”¹-trans-lax : (Ï† : GroupHom G H) (Ïˆ : GroupHom H K) â†’ (ğ”¹-hom Ï† hGpdCat.âˆ™â‚ ğ”¹-hom Ïˆ) â‰¡ ğ”¹-hom (Ï† TwoGroup.âˆ™â‚ Ïˆ)
    ğ”¹-trans-lax {G} {H} {K} Ï† Ïˆ = funExt (Delooping.elimSet âŸ¨ G âŸ© (str G) isSetMotive refl Î» g i j â†’ ğ”¹K.loop ((Ï† TwoGroup.âˆ™â‚ Ïˆ) .fst g) i) where
      module ğ”¹G = Delooping.ğ”¹ âŸ¨ G âŸ© (str G)
      module ğ”¹K = Delooping.ğ”¹ âŸ¨ K âŸ© (str K)

      isSetMotive : (x : Delooping.ğ”¹ âŸ¨ G âŸ© (str G)) â†’ isSet ((ğ”¹-hom Ïˆ $ ğ”¹-hom Ï† x) â‰¡ (ğ”¹-hom (Ï† TwoGroup.âˆ™â‚ Ïˆ) x))
      isSetMotive x = ğ”¹K.isGroupoidğ”¹ _ _

    ğ”¹-trans-lax-natural : {Ï†â‚ Ï†â‚‚ : GroupHom G H} {Ïˆâ‚ Ïˆâ‚‚ : GroupHom H K}
      â†’ (h : Conjugator Ï†â‚ Ï†â‚‚)
      â†’ (k : Conjugator Ïˆâ‚ Ïˆâ‚‚)
      â†’ ((ğ”¹-rel h hGpdCat.âˆ™â‚• ğ”¹-rel k) âˆ™ ğ”¹-trans-lax Ï†â‚‚ Ïˆâ‚‚) â‰¡ (ğ”¹-trans-lax Ï†â‚ Ïˆâ‚ âˆ™ ğ”¹-rel (h TwoGroup.âˆ™â‚• k))
    ğ”¹-trans-lax-natural {G} {H} {K} {Ï†â‚} {Ï†â‚‚} {Ïˆâ‚} {Ïˆâ‚‚} h k = funExtSquare _ _ _ _ lax where
      module K = GroupStr (str K)
      ğ”¹G = Delooping.ğ”¹ âŸ¨ G âŸ© (str G)
      ğ”¹H = Delooping.ğ”¹ âŸ¨ H âŸ© (str H)
      ğ”¹K = Delooping.ğ”¹ âŸ¨ K âŸ© (str K)
      module ğ”¹G = Delooping âŸ¨ G âŸ© (str G)
      module ğ”¹H = Delooping âŸ¨ H âŸ© (str H)
      module ğ”¹K = Delooping âŸ¨ K âŸ© (str K)

      open ğ”¹G using (congâ‹† ; congâ‹†-âˆ™)

      -- The meat of the proof: Horizontal composition computes to the correct loop at the point.
      -- This is almost definitional, except that the LHS computes to the diagonal of a composite square,
      -- in particular it is the diagonal that shows that the group element underlying `(h TwoGroup.âˆ™â‚• k)`
      -- is a conjugator of Ïˆâ‚ and Ïˆâ‚‚.
      lemma : congâ‹† (ğ”¹-rel h hGpdCat.âˆ™â‚• ğ”¹-rel k) â‰¡ ğ”¹K.loop ((h TwoGroup.âˆ™â‚• k) .fst)
      lemma = Map.mapâ‰¡-loopáµ Ïˆâ‚ Ïˆâ‚‚ k (h .fst)

      laxâ‹† : congâ‹† (((ğ”¹-rel h hGpdCat.âˆ™â‚• ğ”¹-rel k) âˆ™ ğ”¹-trans-lax Ï†â‚‚ Ïˆâ‚‚)) â‰¡ congâ‹† (ğ”¹-trans-lax Ï†â‚ Ïˆâ‚ âˆ™ ğ”¹-rel (h TwoGroup.âˆ™â‚• k))
      laxâ‹† =
        congâ‹† (((ğ”¹-rel h hGpdCat.âˆ™â‚• ğ”¹-rel k) âˆ™ ğ”¹-trans-lax Ï†â‚‚ Ïˆâ‚‚))      â‰¡âŸ¨ congâ‹†-âˆ™ (ğ”¹-rel h hGpdCat.âˆ™â‚• ğ”¹-rel k) (ğ”¹-trans-lax Ï†â‚‚ Ïˆâ‚‚) âŸ©
        congâ‹† (ğ”¹-rel h hGpdCat.âˆ™â‚• ğ”¹-rel k) âˆ™ congâ‹† (ğ”¹-trans-lax Ï†â‚‚ Ïˆâ‚‚)  â‰¡âŸ¨âŸ©
        congâ‹† (ğ”¹-rel h hGpdCat.âˆ™â‚• ğ”¹-rel k) âˆ™ refl                       â‰¡âŸ¨ sym $ GL.rUnit _ âŸ©
        congâ‹† (ğ”¹-rel h hGpdCat.âˆ™â‚• ğ”¹-rel k)                              â‰¡âŸ¨ lemma âŸ©
        ğ”¹K.loop ((h TwoGroup.âˆ™â‚• k) .fst)                                â‰¡âŸ¨âŸ©
        congâ‹† (ğ”¹-rel (h TwoGroup.âˆ™â‚• k))                                 â‰¡âŸ¨ GL.lUnit _ âŸ©
        refl âˆ™ congâ‹† (ğ”¹-rel (h TwoGroup.âˆ™â‚• k))                          â‰¡âŸ¨âŸ©
        congâ‹† (ğ”¹-trans-lax Ï†â‚ Ïˆâ‚) âˆ™ congâ‹† (ğ”¹-rel (h TwoGroup.âˆ™â‚• k))     â‰¡âŸ¨ sym $ congâ‹†-âˆ™ (ğ”¹-trans-lax Ï†â‚ Ïˆâ‚) (ğ”¹-rel (h TwoGroup.âˆ™â‚• k)) âŸ©
        congâ‹† (ğ”¹-trans-lax Ï†â‚ Ïˆâ‚ âˆ™ ğ”¹-rel (h TwoGroup.âˆ™â‚• k)) âˆ

      lax : (x : ğ”¹G) â†’ (((ğ”¹-rel h hGpdCat.âˆ™â‚• ğ”¹-rel k) âˆ™ ğ”¹-trans-lax Ï†â‚‚ Ïˆâ‚‚) â‰¡$S x) â‰¡ (ğ”¹-trans-lax Ï†â‚ Ïˆâ‚ âˆ™ ğ”¹-rel (h TwoGroup.âˆ™â‚• k) â‰¡$S x)
      lax = ğ”¹G.elimProp (Î» x â†’ ğ”¹K.isGroupoidğ”¹ _ _ _ _) laxâ‹†

    ğ”¹-id-lax : (G : Group â„“) â†’ id âŸ¨ ğ”¹-ob G âŸ© â‰¡ ğ”¹-hom (idGroupHom {G = G})
    ğ”¹-id-lax G = sym (Map.map-id G)

    ğ”¹-assoc : (Ï† : GroupHom G H) (Ïˆ : GroupHom H K) (Ï : GroupHom K L)
      â†’ Square
        ((ğ”¹-trans-lax Ï† Ïˆ hGpdCat.âˆ™â‚• reflâ€² (ğ”¹-hom Ï)) âˆ™ ğ”¹-trans-lax (Ï† TwoGroup.âˆ™â‚ Ïˆ) Ï)
        ((reflâ€² (ğ”¹-hom Ï†) hGpdCat.âˆ™â‚• ğ”¹-trans-lax Ïˆ Ï) âˆ™ ğ”¹-trans-lax Ï† (Ïˆ TwoGroup.âˆ™â‚ Ï))
        (reflâ€² ((ğ”¹-hom Ï† hGpdCat.âˆ™â‚ ğ”¹-hom Ïˆ) hGpdCat.âˆ™â‚ ğ”¹-hom Ï))
        (cong ğ”¹-hom (TwoGroup.comp-hom-assoc Ï† Ïˆ Ï))
    ğ”¹-assoc {G} {H} {L} Ï† Ïˆ Ï = funExtSquare _ _ _ _ assoc-ext where
      ğ”¹G = Delooping.ğ”¹ âŸ¨ G âŸ© (str G)
      ğ”¹L = Delooping.ğ”¹ âŸ¨ L âŸ© (str L)
      module ğ”¹G = Delooping âŸ¨ G âŸ© (str G)
      module ğ”¹L = Delooping âŸ¨ L âŸ© (str L)

      open ğ”¹G using (congâ‹† ; congâ‹†-âˆ™)

      assoc-ext : (x : ğ”¹G) â†’ Square
        ((ğ”¹-trans-lax Ï† Ïˆ hGpdCat.âˆ™â‚• reflâ€² (ğ”¹-hom Ï)) âˆ™ ğ”¹-trans-lax (Ï† TwoGroup.âˆ™â‚ Ïˆ) Ï â‰¡$ x)
        (((reflâ€² (ğ”¹-hom Ï†) hGpdCat.âˆ™â‚• ğ”¹-trans-lax Ïˆ Ï) âˆ™ ğ”¹-trans-lax Ï† (Ïˆ TwoGroup.âˆ™â‚ Ï)) â‰¡$ x)
        refl
        (Î» i â†’ ğ”¹-hom (TwoGroup.comp-hom-assoc Ï† Ïˆ Ï i) x)
      assoc-ext = ğ”¹G.elimProp (Î» x â†’ ğ”¹L.isPropDeloopingSquare) $
        let
          p = (ğ”¹-trans-lax Ï† Ïˆ hGpdCat.âˆ™â‚• reflâ€² (ğ”¹-hom Ï))
          q = (ğ”¹-trans-lax (Ï† TwoGroup.âˆ™â‚ Ïˆ) Ï)
          r = (reflâ€² (ğ”¹-hom Ï†) hGpdCat.âˆ™â‚• ğ”¹-trans-lax Ïˆ Ï)
          s = (ğ”¹-trans-lax Ï† (Ïˆ TwoGroup.âˆ™â‚ Ï))
        in
        (congâ‹† $ p âˆ™ q)     â‰¡âŸ¨ congâ‹†-âˆ™ p q âŸ©
        (congâ‹† p âˆ™ congâ‹† q) â‰¡âŸ¨âŸ©
        (refl âˆ™ refl)       â‰¡âŸ¨ sym $ congâ‹†-âˆ™ r s âŸ©
        (congâ‹† $ r âˆ™ s)     âˆ

    ğ”¹-unit-left : (Ï† : GroupHom G H) â†’ Square
      ((ğ”¹-id-lax G hGpdCat.âˆ™â‚• reflâ€² (ğ”¹-hom Ï†)) hGpdCat.âˆ™áµ¥ ğ”¹-trans-lax idGroupHom Ï†)
      (reflâ€² (ğ”¹-hom Ï†))
      (hGpdCat.comp-hom-unit-left (ğ”¹-hom Ï†))
      (cong ğ”¹-hom (TwoGroup.comp-hom-unit-left Ï†))
    ğ”¹-unit-left {G} {H} Ï† = funExtSquare _ _ _ _ $ ğ”¹G.elimProp (Î» _ â†’ ğ”¹H.isPropDeloopingSquare) unit-leftâ‹† where
      module ğ”¹G = Delooping âŸ¨ G âŸ© (str G)
      module ğ”¹H = Delooping âŸ¨ H âŸ© (str H)

      p : (id âŸ¨ ğ”¹-ob G âŸ©) â‹† (ğ”¹-hom Ï†) â‰¡ (ğ”¹-hom idGroupHom) â‹† (ğ”¹-hom Ï†)
      p = ğ”¹-id-lax G hGpdCat.âˆ™â‚• reflâ€² (ğ”¹-hom Ï†)

      q : (ğ”¹-hom idGroupHom â‹† ğ”¹-hom Ï†) â‰¡ ğ”¹-hom (idGroupHom TwoGroup.âˆ™â‚ Ï†)
      q = ğ”¹-trans-lax idGroupHom Ï†

      unit-leftâ‹† : ğ”¹G.congâ‹† (p âˆ™ q) â‰¡ reflâ€² ğ”¹H.â‹†
      unit-leftâ‹† = ğ”¹G.congâ‹†-âˆ™ p q âˆ™ sym compPathRefl

    ğ”¹-unit-right : (Ï† : GroupHom G H) â†’ Square
      ((reflâ€² (ğ”¹-hom Ï†) hGpdCat.âˆ™â‚• ğ”¹-id-lax H) hGpdCat.âˆ™áµ¥ ğ”¹-trans-lax Ï† idGroupHom)
      (reflâ€² (ğ”¹-hom Ï†))
      (hGpdCat.comp-hom-unit-right (ğ”¹-hom Ï†))
      (cong ğ”¹-hom (TwoGroup.comp-hom-unit-right Ï†))
    ğ”¹-unit-right {G} {H} Ï† = funExtSquare _ _ _ _ $ ğ”¹G.elimProp (Î» _ â†’ ğ”¹H.isPropDeloopingSquare) unit-rightâ‹† where
      module ğ”¹G = Delooping âŸ¨ G âŸ© (str G)
      module ğ”¹H = Delooping âŸ¨ H âŸ© (str H)

      p = reflâ€² (ğ”¹-hom Ï†) hGpdCat.âˆ™â‚• ğ”¹-id-lax H
      q = ğ”¹-trans-lax Ï† idGroupHom

      -- Both p and q reduce to refl at the point:
      unit-rightâ‹† : ğ”¹G.congâ‹† (p âˆ™ q) â‰¡ reflâ€² ğ”¹H.â‹†
      unit-rightâ‹† = ğ”¹G.congâ‹†-âˆ™ p q âˆ™ sym compPathRefl

  TwoDelooping : LaxFunctor (TwoGroup â„“) (hGpdCat â„“)
  TwoDelooping .LaxFunctor.F-ob = ğ”¹-ob
  TwoDelooping .LaxFunctor.F-hom = ğ”¹-hom
  TwoDelooping .LaxFunctor.F-rel = ğ”¹-rel
  TwoDelooping .LaxFunctor.F-rel-id = ğ”¹-rel-id
  TwoDelooping .LaxFunctor.F-rel-trans = ğ”¹-rel-trans
  TwoDelooping .LaxFunctor.F-trans-lax = ğ”¹-trans-lax
  TwoDelooping .LaxFunctor.F-trans-lax-natural = ğ”¹-trans-lax-natural
  TwoDelooping .LaxFunctor.F-id-lax = ğ”¹-id-lax
  TwoDelooping .LaxFunctor.F-assoc = ğ”¹-assoc
  TwoDelooping .LaxFunctor.F-unit-left = ğ”¹-unit-left
  TwoDelooping .LaxFunctor.F-unit-right = ğ”¹-unit-right

  module _ (G H : TwoGroup.ob) where
    private
      Group[_,_] = LocalCategory (TwoGroup â„“)
      hGpd[_,_] = LocalCategory (hGpdCat â„“)

      TwoDelooping[_,_] = LocalFunctor TwoDelooping

    isLocallyFullyFaithfulDelooping : Functor.isFullyFaithful TwoDelooping[ G , H ]
    isLocallyFullyFaithfulDelooping = goal where module _ (Ï† Ïˆ : TwoGroup.hom G H) where
      goal : isEquiv ğ”¹-rel
      goal = equivIsEquiv (MapPathEquiv.mapâ‰¡Equiv Ï† Ïˆ)

    isEssentiallySurjLocalDelooping : Functor.isEssentiallySurj TwoDelooping[ G , H ]
    isEssentiallySurjLocalDelooping = goal where module _ (f : âŸ¨ ğ”¹-ob G âŸ© â†’ âŸ¨ ğ”¹-ob H âŸ©) where
      open import Cubical.HITs.PropositionalTruncation.Monad
      goal : âˆƒ[ Ï† âˆˆ GroupHom G H ] CatIso hGpd[ _ , _ ] (map Ï†) f
      goal = do
        (Ï† , section-f-mapÏ†) â† LocalInverse.isSurjection-map f
        âˆƒ-intro Ï† $ pathToIso section-f-mapÏ†

    isLocalWeakEquivalenceDelooping : isWeakEquivalence TwoDelooping[ G , H ]
    isLocalWeakEquivalenceDelooping .isWeakEquivalence.fullfaith = isLocallyFullyFaithfulDelooping
    isLocalWeakEquivalenceDelooping .isWeakEquivalence.esssurj = isEssentiallySurjLocalDelooping

    LocalWeakEquivalence : WeakEquivalence Group[ G , H ] hGpd[ ğ”¹-ob G , ğ”¹-ob H ]
    LocalWeakEquivalence .WeakEquivalence.func = TwoDelooping[ G , H ]
    LocalWeakEquivalence .WeakEquivalence.isWeakEquiv = isLocalWeakEquivalenceDelooping
