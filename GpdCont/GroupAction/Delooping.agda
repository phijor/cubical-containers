{-# OPTIONS --lossy-unification #-}
module GpdCont.GroupAction.Delooping where

open import GpdCont.Prelude
open import GpdCont.Univalence using (ua ; uaâ†’)
open import GpdCont.Connectivity using (isPathConnected)

open import GpdCont.GroupAction.Base using (Action ; _âº_ ; module ActionProperties)
open import GpdCont.GroupAction.Equivariant renaming (isEquivariantMap[_][_,_] to isEquivariantMap)
open import GpdCont.GroupAction.TwoCategory using (GroupActioná´° ; GroupAction)
open import GpdCont.GroupAction.AssociatedBundle using (associatedBundle ; associatedBundleMap ; associatedBundleMapEquiv)
open import GpdCont.Group.TwoCategory using (TwoGroup)

open import GpdCont.SetBundle.Base using (SetBundle ; SetBundleá´° ; SetBundleáµ€ ; isLocallyGroupoidalSetBundle ; module SetBundleNotation)

import      GpdCont.Delooping as Delooping
open import GpdCont.Delooping.Functor using (module TwoFunc ; module LocalInverse)
import      GpdCont.Delooping.Map as DeloopingMap

open import GpdCont.TwoCategory.Base using (TwoCategory)
open import GpdCont.TwoCategory.LaxFunctor using (LaxFunctor)
open import GpdCont.TwoCategory.StrictFunctor using (StrictFunctor)
open import GpdCont.TwoCategory.Pseudofunctor using (isPseudoFunctor ; isLocallyGroupoidalâ†’isPseudofunctor)
open import GpdCont.TwoCategory.LocalCategory using (LocalCategory)
open import GpdCont.TwoCategory.LocalFunctor as LocalFunctor using (isLocallyFullyFaithful ; isLocallyEssentiallySurjective ; isLocallyWeakEquivalence)
open import GpdCont.TwoCategory.Displayed.Base using (TwoCategoryá´°)
open import GpdCont.TwoCategory.Displayed.LaxFunctor using (LaxFunctorá´°)
open import GpdCont.TwoCategory.Displayed.StrictFunctor using (StrictFunctorá´°)
open import GpdCont.TwoCategory.Displayed.LocallyThin using (IntoLocallyThin)
open import GpdCont.TwoCategory.HomotopyGroupoid using (hGpdCat)

open import Cubical.Foundations.Equiv as Equiv using (isEquiv ; equivFun ; equivIsEquiv ; fiber ; invEq ; _âˆ™â‚‘_)
open import Cubical.Foundations.HLevels using (isOfHLevelPathP' ; isSetâ†’ ; isSetâ†’SquareP)
open import Cubical.Foundations.Path using (compPathâ†’Square)
open import Cubical.Foundations.Transport using (substâ» ; substâ»-filler ; substCommSlice)
open import Cubical.Functions.FunExtEquiv using (funExtEquiv ; funExtDep)
import      Cubical.Data.Sigma as Sigma
open import Cubical.Algebra.Group.MorphismProperties using (GroupHomâ‰¡)

{-# INJECTIVE_FOR_INFERENCE âŸ¨_âŸ© #-}

-- Delooping of group actions into set bundles as a functor of 2-categories.
-- =========================================================================
--
-- We define this functor by extending the delooping-functor of groups to
-- a lax functor on total 2-categories
--
--    ğ”¹ : âˆ« Group GroupActioná´° â†’ âˆ« hGpd SetBundleá´°
--
-- In particular, we only need to give the "displayed" part of the functor,
-- and since its target has locally thin displayed 2-cells, it suffices to
-- give the following data:
--
--  âˆ™ ğ”¹á´°â‚€, assigning to a group action its associated bundle,
--  âˆ™ ğ”¹á´°â‚, assigning to an equivariant map its associated map of bundles
--  âˆ™ ğ”¹á´°â‚‚, assigning to a conjugator of actions a homotopy of bundle maps
--
-- ...and proofs that ğ”¹ (laxly) preserves identity 2-cells and vertical composites.
module _ (â„“ : Level) where
  private
    module Group = TwoCategory (TwoGroup â„“)
    module GroupAction = TwoCategory (GroupAction â„“)
    module GroupActioná´° = TwoCategoryá´° (GroupActioná´° â„“)
    module hGpdCat = TwoCategory (hGpdCat â„“)

    module SetBundle = SetBundleNotation â„“
    -- module SetBundleá´° = TwoCategoryá´° (SetBundleá´° â„“)

    open Delooping.ğ”¹ using (â‹† ; loop)
    ğ”¹ = TwoFunc.TwoDeloopingË¢ â„“
    module ğ”¹ = StrictFunctor ğ”¹

    ğ”¹-lax = TwoFunc.TwoDelooping â„“
    module ğ”¹-lax = LaxFunctor ğ”¹-lax

    -- To each group action, assign its associated bundle:
    ğ”¹á´°â‚€ : âˆ€ {G} â†’ GroupActioná´°.ob[ G ] â†’ SetBundle.ob[ ğ”¹.â‚€ G ]
    ğ”¹á´°â‚€ (X , Ïƒ) = associatedBundle {X = X} Ïƒ
    -- {-# INJECTIVE_FOR_INFERENCE ğ”¹á´°â‚€ #-}

  -- Any equivariant map of group actions is exactly a map of associated bundles.
  ğ”¹á´°â‚-equiv : âˆ€ {G H} {Ï† : Group.hom G H} {Xá´³ : GroupActioná´°.ob[ G ]} {Yá´´ : GroupActioná´°.ob[ H ]}
    â†’ GroupActioná´°.hom[ Ï† ] Xá´³ Yá´´ â‰ƒ SetBundle.hom[ ğ”¹.â‚ Ï† ] (ğ”¹á´°â‚€ Xá´³) (ğ”¹á´°â‚€ Yá´´)
  ğ”¹á´°â‚-equiv {Ï†} {Xá´³ = _ , Ïƒ} {Yá´´ = _ , Ï„} = associatedBundleMapEquiv Ïƒ Ï„ Ï†

  private
    ğ”¹á´°â‚ : âˆ€ {G H} {Ï† : Group.hom G H} {Xá´³ : GroupActioná´°.ob[ G ]} {Yá´´ : GroupActioná´°.ob[ H ]}
      â†’ GroupActioná´°.hom[ Ï† ] Xá´³ Yá´´
      â†’ SetBundle.hom[ ğ”¹.â‚ Ï† ] (ğ”¹á´°â‚€ Xá´³) (ğ”¹á´°â‚€ Yá´´)
    ğ”¹á´°â‚ = equivFun ğ”¹á´°â‚-equiv
    {-# INJECTIVE_FOR_INFERENCE ğ”¹á´°â‚ #-}

    ğ”¹á´°â‚â»Â¹ : âˆ€ {G H} {Ï† : Group.hom G H} {Xá´³ : GroupActioná´°.ob[ G ]} {Yá´´ : GroupActioná´°.ob[ H ]}
      â†’ SetBundle.hom[ ğ”¹.â‚ Ï† ] (ğ”¹á´°â‚€ Xá´³) (ğ”¹á´°â‚€ Yá´´)
      â†’ GroupActioná´°.hom[ Ï† ] Xá´³ Yá´´
    ğ”¹á´°â‚â»Â¹ = invEq ğ”¹á´°â‚-equiv

  isEquiv-ğ”¹á´°â‚ : âˆ€ {G H} {Ï† : Group.hom G H} {Xá´³ : GroupActioná´°.ob[ G ]} {Yá´´ : GroupActioná´°.ob[ H ]}
    â†’ isEquiv (ğ”¹á´°â‚ {G} {H} {Ï†} {Xá´³} {Yá´´})
  isEquiv-ğ”¹á´°â‚ = equivIsEquiv ğ”¹á´°â‚-equiv

  private
    -- Path lemma characterizing displayed homotopies of set bundle maps
    -- with a delooping in their codomain.  Such homotopies are defined
    -- pointwise in the delooping; and since the target is a proposition
    -- (SetBundleá´° is locally thin), it suffices to give the homotopy on
    -- the point.
    module _
      {G} {Y} {f g : hGpdCat.hom (ğ”¹.â‚€ G) Y} {r : hGpdCat.rel f g}
      {Xá´³ : GroupActioná´°.ob[ G ]}
      (yá´° : SetBundle.ob[ Y ])
      {fá´° : SetBundle.hom[ f ] (ğ”¹á´°â‚€ Xá´³) yá´°}
      {gá´° : SetBundle.hom[ g ] (ğ”¹á´°â‚€ Xá´³) yá´°}
      where
      open Delooping G using (elimPropEquiv)
      opaque
        isPropğ”¹á´°â‚PathP : âˆ€ x â†’ isProp (PathP (Î» i â†’ âŸ¨ yá´° (r i x) âŸ© â†’ âŸ¨ ğ”¹á´°â‚€ Xá´³ x âŸ©) (fá´° x) (gá´° x))
        isPropğ”¹á´°â‚PathP x = isOfHLevelPathP' 1 (isSetâ†’ is-set-ğ”¹á´°â‚€) _ _ where
          is-set-ğ”¹á´°â‚€ : isSet âŸ¨ ğ”¹á´°â‚€ Xá´³ x âŸ©
          is-set-ğ”¹á´°â‚€ = str $ ğ”¹á´°â‚€ Xá´³ x

        ğ”¹á´°â‚-PathPEquiv :
          (PathP (Î» i â†’ âŸ¨ yá´° (r i â‹†) âŸ© â†’ âŸ¨ ğ”¹á´°â‚€ Xá´³ â‹† âŸ©) (fá´° â‹†) (gá´° â‹†))
            â‰ƒ
          (âˆ€ x â†’ PathP (Î» i â†’ âŸ¨ yá´° (r i x) âŸ© â†’ âŸ¨ ğ”¹á´°â‚€ Xá´³ x âŸ©) (fá´° x) (gá´° x))
        ğ”¹á´°â‚-PathPEquiv = elimPropEquiv isPropğ”¹á´°â‚PathP

        ğ”¹á´°â‚-PathPâ‰ƒSetBundleRel : (PathP (Î» i â†’ âŸ¨ yá´° (r i â‹†) âŸ© â†’ âŸ¨ ğ”¹á´°â‚€ Xá´³ â‹† âŸ©) (fá´° â‹†) (gá´° â‹†)) â‰ƒ (SetBundle.rel[_] {yá´° = yá´°} r fá´° gá´°)
        ğ”¹á´°â‚-PathPâ‰ƒSetBundleRel = ğ”¹á´°â‚-PathPEquiv âˆ™â‚‘ funExtEquiv
        {-# INJECTIVE_FOR_INFERENCE ğ”¹á´°â‚-PathPâ‰ƒSetBundleRel #-}

        ğ”¹á´°â‚PathP : (pâ‹† : PathP (Î» i â†’ âŸ¨ yá´° (r i â‹†) âŸ© â†’ âŸ¨ ğ”¹á´°â‚€ Xá´³ â‹† âŸ©) (fá´° â‹†) (gá´° â‹†))
          â†’ SetBundle.rel[_] {yá´° = yá´°} r fá´° gá´°
        ğ”¹á´°â‚PathP = equivFun ğ”¹á´°â‚-PathPâ‰ƒSetBundleRel

  -- A conjugator relating two equivariant maps is exactly a homotopy of associated bundle maps.
  -- We define the map underlying this equivalence to be the action of ğ”¹ on 2-cells.
  module _
    {G H} {Ï† Ïˆ : Group.hom G H}
    {r : Group.rel Ï† Ïˆ}
    {Xá´³ : GroupActioná´°.ob[ G ]}
    {Yá´´ @ (Y , Ï„) : GroupActioná´°.ob[ H ]}
    {fá´° @ (f , _) : GroupActioná´°.hom[ Ï† ] Xá´³ Yá´´}
    {gá´° @ (g , _) : GroupActioná´°.hom[ Ïˆ ] Xá´³ Yá´´}
    where

    -- Some `r` is a conjugator of `f` and `g` iff and only if it identifies it identifies
    -- them as a permutation of their domain.
    ğ”¹á´°â‚‚-equiv : (GroupActioná´°.rel[ r ] fá´° gá´°) â‰ƒ (SetBundle.rel[_] {yá´° = ğ”¹á´°â‚€ Yá´´} (ğ”¹.â‚‚ {f = Ï†} {g = Ïˆ} r) (ğ”¹á´°â‚ fá´°) (ğ”¹á´°â‚ gá´°))
    ğ”¹á´°â‚‚-equiv =
      (GroupActioná´°.rel[ r ] fá´° gá´°) â‰ƒâŸ¨ ActionProperties.uaExtEquiv Ï„ {fâ‚€ = f} {fâ‚ = g} (r .fst) âŸ©
      (PathP (Î» i â†’ âŸ¨ ğ”¹á´°â‚€ {H} Yá´´ (ğ”¹.â‚‚ {f = Ï†} {g = Ïˆ} r i â‹†) âŸ© â†’ âŸ¨ ğ”¹á´°â‚€ {G} Xá´³ â‹† âŸ©) f g) â‰ƒâŸ¨ ğ”¹á´°â‚-PathPâ‰ƒSetBundleRel {f = ğ”¹.â‚ Ï†} {g = ğ”¹.â‚ Ïˆ} {r = ğ”¹.â‚‚ r} (ğ”¹á´°â‚€ Yá´´) âŸ©
      (SetBundle.rel[_] {yá´° = ğ”¹á´°â‚€ Yá´´} (ğ”¹.â‚‚ r) (ğ”¹á´°â‚ fá´°) (ğ”¹á´°â‚ gá´°)) â‰ƒâˆ

    ğ”¹á´°â‚‚ : GroupActioná´°.rel[ r ] fá´° gá´° â†’ SetBundle.rel[_] {yá´° = ğ”¹á´°â‚€ Yá´´} (ğ”¹.â‚‚ {f = Ï†} {g = Ïˆ} r) (ğ”¹á´°â‚ fá´°) (ğ”¹á´°â‚ gá´°)
    ğ”¹á´°â‚‚ = equivFun ğ”¹á´°â‚‚-equiv
    {-# INJECTIVE_FOR_INFERENCE ğ”¹á´°â‚‚ #-}

    isEquiv-ğ”¹á´°â‚‚ : isEquiv ğ”¹á´°â‚‚
    isEquiv-ğ”¹á´°â‚‚ = equivIsEquiv ğ”¹á´°â‚‚-equiv

  private
    ğ”¹-rel-id : âˆ€ {x y : Group.ob} {f : Group.hom x y} {xá´° : GroupActioná´°.ob[ x ]} {yá´° : GroupActioná´°.ob[ y ]}
      â†’ (fá´° : GroupActioná´°.hom[ f ] xá´° yá´°)
      â†’ PathP (Î» i â†’ SetBundle.rel[_] {yá´° = ğ”¹á´°â‚€ yá´°} (ğ”¹.F-rel-id {f = f} i) (ğ”¹á´°â‚ fá´°) (ğ”¹á´°â‚ fá´°))
          (ğ”¹á´°â‚‚ {r = Group.id-rel f} (GroupActioná´°.id-relá´° fá´°))
          (SetBundle.id-relá´° {yá´° = ğ”¹á´°â‚€ yá´°} (ğ”¹á´°â‚ fá´°))
    ğ”¹-rel-id {f} {yá´°} fá´° = SetBundle.relá´°â‰¡ {yá´° = ğ”¹á´°â‚€ yá´°} (ğ”¹.F-rel-id {f = f})

    ğ”¹-rel-trans : âˆ€ {x y} {f g h : Group.hom x y} {r : Group.rel f g} {s : Group.rel g h}
      â†’ {xá´° : GroupActioná´°.ob[ x ]} {yá´° : GroupActioná´°.ob[ y ]}
      â†’ {fá´° : GroupActioná´°.hom[ f ] xá´° yá´°}
      â†’ {gá´° : GroupActioná´°.hom[ g ] xá´° yá´°}
      â†’ {há´° : GroupActioná´°.hom[ h ] xá´° yá´°}
      â†’ (rá´° : GroupActioná´°.rel[ r ] fá´° gá´°)
      â†’ (sá´° : GroupActioná´°.rel[ s ] gá´° há´°)
      â†’ PathP (Î» i â†’ SetBundle.rel[_] {yá´° = ğ”¹á´°â‚€ yá´°} (ğ”¹.F-rel-trans r s i) (ğ”¹á´°â‚ fá´°) (ğ”¹á´°â‚ há´°))
          (ğ”¹á´°â‚‚ (GroupActioná´°.transá´° {r = r} {s = s} {fá´° = fá´°} {gá´° = gá´°} {há´° = há´°} rá´° sá´°))
          (SetBundle.transá´° {yá´° = ğ”¹á´°â‚€ yá´°} {gá´° = ğ”¹á´°â‚ gá´°} (ğ”¹á´°â‚‚ rá´°) (ğ”¹á´°â‚‚ sá´°))
    ğ”¹-rel-trans {r} {s} {yá´°} rá´° sá´° = SetBundle.relá´°â‰¡ {r = ğ”¹.â‚‚ (r Group.âˆ™áµ¥ s)} {s = ğ”¹.â‚‚ r hGpdCat.âˆ™áµ¥ ğ”¹.â‚‚ s} {yá´° = ğ”¹á´°â‚€ yá´°} (ğ”¹.F-rel-trans r s)

    ğ”¹-hom-comp : âˆ€ {x y z} {f : Group.hom x y} {g : Group.hom y z}
      â†’ {xá´° : GroupActioná´°.ob[ x ]} {yá´° : GroupActioná´°.ob[ y ]} {zá´° : GroupActioná´°.ob[ z ]}
      â†’ (fá´° : GroupActioná´°.hom[ f ] xá´° yá´°) (gá´° : GroupActioná´°.hom[ g ] yá´° zá´°)
      â†’ PathP (Î» i â†’ SetBundle.hom[ ğ”¹.F-hom-comp f g i ] (ğ”¹á´°â‚€ xá´°) (ğ”¹á´°â‚€ zá´°))
        (SetBundle.comp-homá´° {f = ğ”¹.â‚ f} {g = ğ”¹.â‚ g} {zá´° = ğ”¹á´°â‚€ zá´°} (ğ”¹á´°â‚ fá´°) (ğ”¹á´°â‚ gá´°))
        (ğ”¹á´°â‚ (fá´° GroupActioná´°.âˆ™â‚á´° gá´°))
    ğ”¹-hom-comp {x = G} {z = K} {f} {g} {xá´°} {zá´°} fá´° gá´° = ğ”¹á´°â‚PathP (ğ”¹á´°â‚€ zá´°) refl

    ğ”¹-hom-id : âˆ€ {x} (xá´° : GroupActioná´°.ob[ x ])
      â†’ PathP (Î» i â†’ SetBundle.hom[ ğ”¹.F-hom-id x i ] (ğ”¹á´°â‚€ xá´°) (ğ”¹á´°â‚€ xá´°))
        (SetBundle.id-homá´° (ğ”¹á´°â‚€ xá´°))
        (ğ”¹á´°â‚ (GroupActioná´°.id-homá´° xá´°))
    ğ”¹-hom-id {x} xá´° = ğ”¹á´°â‚PathP (ğ”¹á´°â‚€ xá´°) refl

  private
    -- On the point, ğ”¹ stricly preserves vertical composition of 2-cells...
    ğ”¹-trans-lax : âˆ€ {G H K} {Ï† : Group.hom G H} {Ïˆ : Group.hom H K}
      â†’ {Xá´³ : GroupActioná´°.ob[ G ]}
      â†’ {Yá´´ : GroupActioná´°.ob[ H ]}
      â†’ {Zá´· : GroupActioná´°.ob[ K ]}
      â†’ (fá´° : GroupActioná´°.hom[ Ï† ] Xá´³ Yá´´)
      â†’ (gá´° : GroupActioná´°.hom[ Ïˆ ] Yá´´ Zá´·)
      â†’ SetBundle.rel[_] {yá´° = ğ”¹á´°â‚€ Zá´·} (ğ”¹-lax.F-trans-lax Ï† Ïˆ) (SetBundle.comp-homá´° {zá´° = ğ”¹á´°â‚€ Zá´·} (ğ”¹á´°â‚ fá´°) (ğ”¹á´°â‚ gá´°)) (ğ”¹á´°â‚ (fá´° GroupActioná´°.âˆ™â‚á´° gá´°))
    ğ”¹-trans-lax {Zá´·} (f , _) (g , _) = ğ”¹á´°â‚PathP (ğ”¹á´°â‚€ Zá´·) $ reflâ€² (f âˆ˜ g)

    -- ...and similarly for identity 2-cells:
    ğ”¹-id-lax : âˆ€ {G}
      â†’ (Xá´³ : GroupActioná´°.ob[ G ])
      â†’ SetBundle.rel[_] (ğ”¹-lax.F-id-lax G) (SetBundle.id-homá´° (ğ”¹á´°â‚€ Xá´³)) (ğ”¹á´°â‚ (GroupActioná´°.id-homá´° Xá´³))
    ğ”¹-id-lax = ğ”¹-hom-id

  -- The above data assembles into a lax functor (ğ”¹ : GroupAction â†’ SetBundle).
  ğ”¹áµ€ : IntoLocallyThin ğ”¹-lax (GroupActioná´° â„“) (SetBundleáµ€ â„“)
  ğ”¹áµ€ .IntoLocallyThin.F-obá´° = ğ”¹á´°â‚€
  ğ”¹áµ€ .IntoLocallyThin.F-homá´° = ğ”¹á´°â‚
  ğ”¹áµ€ .IntoLocallyThin.F-relá´° = ğ”¹á´°â‚‚
  ğ”¹áµ€ .IntoLocallyThin.F-trans-laxá´° = ğ”¹-trans-lax
  ğ”¹áµ€ .IntoLocallyThin.F-id-laxá´° = ğ”¹-id-lax

  ğ”¹á´° : LaxFunctorá´° ğ”¹-lax (GroupActioná´° â„“) (SetBundleá´° â„“)
  ğ”¹á´° = IntoLocallyThin.toLaxFunctorá´° ğ”¹áµ€

  Delooping : LaxFunctor (GroupAction â„“) (SetBundle â„“)
  Delooping = LaxFunctorá´°.toTotalFunctor ğ”¹á´°

  ğ”¹á´°Ë¢ : StrictFunctorá´° ğ”¹ (GroupActioná´° â„“) (SetBundleá´° â„“)
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-obá´° = ğ”¹á´°â‚€
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-homá´° = ğ”¹á´°â‚
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-relá´° = ğ”¹á´°â‚‚
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-rel-idá´° = ğ”¹-rel-id
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-rel-transá´° = ğ”¹-rel-trans
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-hom-compá´° = ğ”¹-hom-comp
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-hom-idá´° = ğ”¹-hom-id
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-assoc-filler-leftá´° fá´° gá´° há´° = doubleCompPathPâ†’DoubleCompPathPFiller _ _ _ _
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-assoc-filler-rightá´° fá´° gá´° há´° = doubleCompPathPâ†’DoubleCompPathPFiller _ _ _ _
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-assocá´° {f} {g} {h} {xá´°} {wá´°} fá´° gá´° há´° = isSetâ†’SquareP (Î» i j â†’ SetBundle.isSetHomá´° (ğ”¹.F-assoc f g h i j) (ğ”¹á´°â‚€ xá´°) (ğ”¹á´°â‚€ wá´°)) _ _ _ _
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-unit-left-fillerá´° fá´° = doubleCompPathPâ†’DoubleCompPathPFiller _ _ _ _
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-unit-leftá´° {f} {xá´°} {yá´°} fá´° = isSetâ†’SquareP (Î» i j â†’ SetBundle.isSetHomá´° (ğ”¹.F-unit-left f i j) (ğ”¹á´°â‚€ xá´°) (ğ”¹á´°â‚€ yá´°)) _ _ _ _
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-unit-right-fillerá´° fá´° = doubleCompPathPâ†’DoubleCompPathPFiller _ _ _ _
  ğ”¹á´°Ë¢ .StrictFunctorá´°.F-unit-rightá´° {f} {xá´°} {yá´°} fá´° = isSetâ†’SquareP (Î» i j â†’ SetBundle.isSetHomá´° (ğ”¹.F-unit-right f i j) (ğ”¹á´°â‚€ xá´°) (ğ”¹á´°â‚€ yá´°)) _ _ _ _

  DeloopingË¢ : StrictFunctor (GroupAction â„“) (SetBundle â„“)
  DeloopingË¢ = StrictFunctorá´°.toTotalFunctor ğ”¹á´°Ë¢

  private
    module âˆ«ğ”¹á´° = StrictFunctor DeloopingË¢

  isPseudoFunctorDelooping : isPseudoFunctor Delooping
  isPseudoFunctorDelooping = isLocallyGroupoidalâ†’isPseudofunctor Delooping (isLocallyGroupoidalSetBundle â„“)

  private
    module ğ”¹Act where
      open LaxFunctor Delooping public
      open LocalFunctor Delooping public

  isConnectedDeloopingBase : (Ïƒ : GroupAction.ob) â†’ isPathConnected âŸ¨ SetBundle.Base (âˆ«ğ”¹á´°.â‚€ Ïƒ) âŸ©
  isConnectedDeloopingBase (G , (X , Ïƒ)) = Delooping.isConnectedDelooping G

  isLocallyFullyFaithfulDelooping : ğ”¹Act.isLocallyFullyFaithful
  isLocallyFullyFaithfulDelooping Ïƒ Ï„ f@(Ï† , _) g@(Ïˆ , _) = goal where
    âˆ«ğ”¹â‚ = LaxFunctor.F-hom Delooping

    âˆ«ğ”¹â‚‚ : GroupAction.rel f g â†’ SetBundle.rel (âˆ«ğ”¹â‚ f) (âˆ«ğ”¹â‚ g)
    âˆ«ğ”¹â‚‚ = LaxFunctor.F-rel Delooping {f = f} {g = g}

    âˆ«ğ”¹â‚‚-equiv : GroupAction.rel f g â‰ƒ SetBundle.rel (âˆ«ğ”¹â‚ f) (âˆ«ğ”¹â‚ g)
    âˆ«ğ”¹â‚‚-equiv = Sigma.Î£-cong-equiv
      (TwoFunc.localDeloopingEmbedding â„“ Ï† Ïˆ)
      Â Î» (r : Group.rel Ï† Ïˆ) â†’ ğ”¹á´°â‚‚-equiv {r = r}

    goal : isEquiv âˆ«ğ”¹â‚‚
    goal = equivIsEquiv âˆ«ğ”¹â‚‚-equiv

  module _
    {G H : Group.ob}
    (Xá´³ @ (X , Ïƒ) : GroupActioná´°.ob[ G ])
    (Yá´´ @ (Y , Ï„) : GroupActioná´°.ob[ H ])
    (f : hGpdCat.hom (ğ”¹.â‚€ G) (ğ”¹.â‚€ H))
    (fá´° : SetBundle.hom[ f ] (ğ”¹á´°â‚€ Xá´³) (ğ”¹á´°â‚€ Yá´´))
    (Ï† : Group.hom G H)
    (Ï†-sec : ğ”¹.â‚ Ï† â‰¡ f)
    where
    ğ”¹á´°â‚-sectionOver : Î£[ Ï†á´° âˆˆ GroupActioná´°.hom[ Ï† ] Xá´³ Yá´´ ] PathP (Î» i â†’ SetBundle.hom[ Ï†-sec i ] (ğ”¹á´°â‚€ Xá´³) (ğ”¹á´°â‚€ Yá´´)) (ğ”¹á´°â‚ Ï†á´°) fá´°
    ğ”¹á´°â‚-sectionOver = goal where
      fá´°â€² : SetBundle.hom[ ğ”¹.â‚ Ï† ] (ğ”¹á´°â‚€ Xá´³) (ğ”¹á´°â‚€ Yá´´)
      fá´°â€² = subst (Î» Ï† â†’ SetBundle.hom[ Ï† ] (ğ”¹á´°â‚€ Xá´³) (ğ”¹á´°â‚€ Yá´´)) (sym Ï†-sec) fá´°

      fá´°â€²-filler : PathP (Î» i â†’ SetBundle.hom[ Ï†-sec (~ i) ] (ğ”¹á´°â‚€ Xá´³) (ğ”¹á´°â‚€ Yá´´)) fá´° fá´°â€²
      fá´°â€²-filler = subst-filler (Î» Ï† â†’ SetBundle.hom[ Ï† ] (ğ”¹á´°â‚€ Xá´³) (ğ”¹á´°â‚€ Yá´´)) (sym Ï†-sec) fá´°

      Ï†á´° : GroupActioná´°.hom[ Ï† ] Xá´³ Yá´´
      Ï†á´° = ğ”¹á´°â‚â»Â¹ fá´°â€²

      Ï†á´°-sec : fá´°â€² â‰¡ ğ”¹á´°â‚ Ï†á´°
      Ï†á´°-sec = sym (Equiv.secEq (ğ”¹á´°â‚-equiv {Xá´³ = Xá´³} {Yá´´ = Yá´´}) fá´°â€²)

      goal : Î£ _ _
      goal .fst = Ï†á´°
      goal .snd = symP (subst (PathP _ fá´°) Ï†á´°-sec fá´°â€²-filler)

  isEssentiallySurjectiveDelooping : ğ”¹Act.isLocallyEssentiallySurjective
  isEssentiallySurjectiveDelooping Xá´³@(G , (X , Ïƒ)) Yá´´@(H , (Y , Ï„)) = goal
    where module _ (f* @ (f , fá´°) : SetBundle.hom (ğ”¹Act.â‚€ Xá´³) (ğ”¹Act.â‚€ Yá´´)) where
    open import Cubical.HITs.PropositionalTruncation.Monad
    open import Cubical.Categories.Category.Base using (CatIso ; pathToIso)
    goal : âˆƒ[ Ï†* âˆˆ GroupAction.hom Xá´³ Yá´´ ] CatIso (LocalCategory _ (ğ”¹Act.â‚€ Xá´³) (ğ”¹Act.â‚€ Yá´´)) (ğ”¹Act.â‚ Ï†*) f*
    goal = do
      (Ï† ,Â Ï†-sec) â† LocalInverse.isSurjection-map f
      let (Ï†á´° , Ï†á´°-sec) = ğ”¹á´°â‚-sectionOver (X , Ïƒ) (Y , Ï„) f fá´° Ï† Ï†-sec
      âˆƒ-intro (Ï† , Ï†á´°) $ pathToIso $ Sigma.Î£PathP (Ï†-sec , Ï†á´°-sec)

  isLocallyWeakEquivalenceDelooping : ğ”¹Act.isLocallyWeakEquivalence
  isLocallyWeakEquivalenceDelooping =
    ğ”¹Act.isLocallyFullyFaithfulÃ—EssentiallySurjectiveâ†’isLocallyWeakEquivalence
      isLocallyFullyFaithfulDelooping
      isEssentiallySurjectiveDelooping
